focus_tree = {
	id = gkj
	country = {
		factor = 0
		modifier = {
			add = 10
			tag = GKJ
		}
	}
	default = no
	continuous_focus_position = { x = 100 y = 100 }
	focus = {
		id = GKJ_test
		icon = GFX_goal_GKJ_im_back
		cost = 2.00
		x = 15
		y = -5
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			add_political_power = 50
			add_country_leader_trait = JEB_revenge
			news_event = { id = News_Events.100 hours = 6 }
		}

	}
	
	focus = {
		id = GKJ_alert_the_people
		icon = GFX_goal_GKJ_tell_the_people
		cost = 3.00
		x = 0
		y = 1
		relative_position_id = GKJ_test
		prerequisite = {
			focus = GKJ_test
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			add_popularity = {
			    ideology = autocratic
			    popularity = 0.10
			}
			add_stability = 0.05
		}

	}
	
	focus = {
		id = GKJ_stabilize_the_government
		icon = GFX_goal_GKJ_stabilize_the_government
		cost = 5.00
		x = 0
		y = 1
		relative_position_id = GKJ_alert_the_people
		prerequisite = {
			focus = GKJ_alert_the_people
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			remove_ideas = {
				GKJ_unstable_gov
			}
		}

	}
	
	focus = {
		id = GKJ_address_the_military
		icon = GFX_goal_GKJ_address_the_army
		cost = 5.00
		x = 3
		y = 1
		relative_position_id = GKJ_stabilize_the_government
		prerequisite = {
			focus = GKJ_stabilize_the_government
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			army_experience = 10
			remove_ideas = {
				GKJ_disorganized_military
			}
				FLO = {
					every_unit_leader = {
						set_nationality = GKJ
					}
				}
		}

	}
	
	focus = {
		id = GKJ_spur_the_workforce
		icon = GFX_goal_GKJ_spur_workforce
		cost = 5.00
		x = 0
		y = 1
		relative_position_id = GKJ_stabilize_the_government
		prerequisite = {
			focus = GKJ_stabilize_the_government
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			remove_ideas = {
				GKJ_crippled_industrial_sector
			}
		}

	}
	
	focus = {
		id = GKJ_motivate_the_people
		icon = GFX_goal_GKJ_inspire_the_people
		cost = 2.00
		x = -3
		y = 1
		relative_position_id = GKJ_stabilize_the_government
		prerequisite = {
			focus = GKJ_stabilize_the_government
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			add_war_support = 0.05
			add_political_power = 100
		}

	}
	
	focus = {
		id = GKJ_by_his_will
		icon = GFX_goal_GKJ_by_his_will
		cost = 3.00
		x = 3
		y = 1
		relative_position_id = GKJ_motivate_the_people
		prerequisite = {
			focus = GKJ_motivate_the_people
		}
		prerequisite = {
			focus = GKJ_spur_the_workforce
		}
		prerequisite = {
			focus = GKJ_address_the_military
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			set_province_name = { id = 3280 name = "New Jerusalem"}
			add_country_leader_trait = JEB_divine_right
		}

	}
	
	focus = {
		id = GKJ_the_new_jerusalem_speech
		icon = GFX_goal_GKJ_new_speech
		cost = 2.00
		x = 0
		y = 2
		relative_position_id = GKJ_by_his_will
		prerequisite = {
			focus = GKJ_by_his_will
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			country_event = { id = gkj.7 }			
		}

	}
	
	focus = {
		id = GKJ_my_time_is_done
		icon = GFX_goal_GKJ_my_time_is_done
		cost = 2.00
		x = -10
		y = 1
		relative_position_id = GKJ_the_new_jerusalem_speech
		available = {
			always = no
		}
		prerequisite = {
			focus = GKJ_the_new_jerusalem_speech
		}
		mutually_exclusive = {  focus = GKJ_my_time_has_just_begun  }
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			GKJ_vacant_leader = {
				promote_character = yes
			}
		}

	}
	
	focus = {
		id = GKJ_wait_he_just_left
		icon = GFX_goal_GKJ_wait_he_just_left
		cost = 2.00
		x = 0
		y = 2
		relative_position_id = GKJ_my_time_is_done
		prerequisite = {
			focus = GKJ_my_time_is_done
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			add_stability = -0.25
			add_political_power = -100
		}

	}
	
	focus = {
		id = GKJ_everyone_is_going_mad
		icon = GFX_goal_GKJ_going_mad
		cost = 3.00
		x = 0
		y = 1
		relative_position_id = GKJ_wait_he_just_left
		prerequisite = {
			focus = GKJ_wait_he_just_left
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			add_ideas = {
				GKJ_madness
			}
		}

	}
	
	focus = {
		id = GKJ_try_to_pacify_the_north
		icon = GFX_goal_GKJ_pacify_north
		cost = 5.00
		x = -4
		y = 1
		relative_position_id = GKJ_everyone_is_going_mad
		prerequisite = {
			focus = GKJ_everyone_is_going_mad
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			country_event = { id = gkj.3 }
		}

	}
	
	focus = {
		id = GKJ_try_to_pacify_the_south
		icon = GFX_goal_GKJ_pacify_south
		cost = 5.00
		x = -1
		y = 1
		relative_position_id = GKJ_everyone_is_going_mad
		prerequisite = {
			focus = GKJ_everyone_is_going_mad
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			country_event = { id = gkj.4 }
		}

	}
	
	focus = {
		id = GKJ_try_to_pacify_the_east
		icon = GFX_goal_GKJ_pacify_east
		cost = 5.00
		x = 1
		y = 1
		relative_position_id = GKJ_everyone_is_going_mad
		prerequisite = {
			focus = GKJ_everyone_is_going_mad
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			country_event = { id = gkj.5 }
		}

	}
	
	focus = {
		id = GKJ_try_to_pacify_the_west
		icon = GFX_goal_GKJ_pacify_west
		cost = 5.00
		x = 4
		y = 1
		relative_position_id = GKJ_everyone_is_going_mad
		prerequisite = {
			focus = GKJ_everyone_is_going_mad
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			country_event = { id = gkj.6 }
		}

	}
	
	focus = {
		id = GKJ_try_to_hold_a_vote
		icon = GFX_goal_GKJ_vote
		cost = 3.00
		x = -4
		y = 2
		relative_position_id = GKJ_try_to_pacify_the_west
		prerequisite = {
			focus = GKJ_try_to_pacify_the_west
		}
		prerequisite = {
			focus = GKJ_try_to_pacify_the_north
		}
		prerequisite = {
			focus = GKJ_try_to_pacify_the_south
		}
		prerequisite = {
			focus = GKJ_try_to_pacify_the_east
		}
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			hidden_effect = {
				country_event = {
					id = gkj.2
					hours = 8
				}
			}
		}

	}
	
	focus = {
		id = GKJ_my_time_has_just_begun
		icon = GFX_goal_generic_funeral_icon
		cost = 2.00
		x = 10
		y = 1
		relative_position_id = GKJ_the_new_jerusalem_speech
		available = {
			always = no
		}
		prerequisite = {
			focus = GKJ_the_new_jerusalem_speech
		}
		mutually_exclusive = {  focus = GKJ_my_time_is_done }
		ai_will_do = { 
			factor = 300
		}
		completion_reward = {
			
		}

	}
}
